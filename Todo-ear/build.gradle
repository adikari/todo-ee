apply plugin: "ear"

description = "Todo-ear"

def domain = "domain1"
def asadminBin = "/Applications/NetBeans/glassfish-4.1.1/glassfish/bin"
def adminUser = "admin"
def passwordFile = "../passwordfile"

dependencies {
  deploy project(":Todo-ejb")
  deploy project(path: ":Todo-web", configuration: "archives")

  add ("earlib", project(":Todo-ri")) {
    transitive = false
  }
}

task deploy(dependsOn: "ear", type: Exec) {
  group = 'Build'
  description = 'Build ear and deploy to glassfish server'

  commandLine "asadmin"

  args "deploy", "--force=true", "--user=${adminUser}", "--passwordfile=${passwordFile}", "${ear.archivePath}"

  standardOutput = new ByteArrayOutputStream()

  ext.output = {
    return standardOutput.toString()
  }

  doLast {
    if(execResult.exitValue == 1) {
      logger.warn(deploy.output())
    }
  }

}

task startServer(type: Exec) {
  group = 'Build Setup'
  description = 'Start glassfish server'

  ignoreExitValue true

  commandLine "${asadminBin}/asadmin"
  args "start-domain", "--debug", "${domain}"

  standardOutput = new ByteArrayOutputStream()

  ext.output = {
    return standardOutput.toString()
  }

  doLast {
    if(execResult.exitValue == 1) {
      logger.warn(startServer.output())
    }
  }
}


