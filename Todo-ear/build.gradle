apply plugin: "ear"

description = "Todo-ear"

dependencies {
  deploy project(":Todo-ejb")
  deploy project(path: ":Todo-web", configuration: "archives")

  add ("earlib", project(":Todo-ri")) {
    transitive = false
  }
}

task deploy(dependsOn: "ear", type: Exec) {
  group = 'Build'
  description = 'Build ear and deploy to glassfish server'

  commandLine "asadmin"

  args "deploy", "--force=true", "--user=$gfAdmin", "--passwordfile=$gfPasswordFile", "$ear.archivePath"

  standardOutput = new ByteArrayOutputStream()

  ext.output = {
    return standardOutput.toString()
  }

  doLast {
    if(execResult.exitValue == 1) {
      logger.warn(deploy.output())
    }
  }

}

task startServer(type: Exec) {
  group = 'Build Setup'
  description = 'Start glassfish server'

  ignoreExitValue true

  commandLine "asadmin"
  args "start-domain", "--debug", "$gfDomain"

  standardOutput = new ByteArrayOutputStream()

  ext.output = {
    return standardOutput.toString()
  }

  doLast {
    if(execResult.exitValue == 1) {
      logger.warn(startServer.output())
    }
  }
}


